#define BAUDRATE 115200
#define TIMEOUT_SEGURIDAD 45000
#define MINIMO 30

#define DEBUG TRUE

// Pines L298N
const int R_IN1 = 2;
const int R_IN2 = 3;

const int L_IN3 = 4;
const int L_IN4 = 7;

const int R_ENA = 5;
const int L_ENB = 6;

// Pines receptor RC
const int PIN_THROTTLE = 8;
const int PIN_STEERING = 9;

void setup() {
  pinMode(R_IN1, OUTPUT);
  pinMode(R_IN2, OUTPUT);
  pinMode(L_IN3, OUTPUT);
  pinMode(L_IN4, OUTPUT);

  // Dirección fija hacia adelante
  digitalWrite(R_IN1, HIGH);
  digitalWrite(R_IN2, LOW);
  digitalWrite(L_IN3, HIGH);
  digitalWrite(L_IN4, LOW);

  // Pines de entrada
  pinMode(PIN_THROTTLE, INPUT);
  pinMode(PIN_STEERING, INPUT);

#ifdef DEBUG
  Serial.begin(BAUDRATE);
  Serial.println("Rover Oruga 1 MK1");
  Serial.println("Ejecutando Programa Control v 1.0");
#endif
}

void stopMotors() {
  analogWrite(R_ENA, 0);
  analogWrite(L_ENB, 0);
}

void loop() {
  // Leer señales con timeout de seguridad
  int pwmThrottle = 0;  
  int pwmSteering = 0;
  updateRCInputs(pwmThrottle, pwmSteering);
  controlFunction(pwmThrottle,pwmSteering);
}

void updateRCInputs(int &throttle_pwm, int &steering_pwm) {
  throttle_pwm = pulseIn(PIN_THROTTLE, HIGH, TIMEOUT_SEGURIDAD);
  steering_pwm = pulseIn(PIN_STEERING, HIGH, TIMEOUT_SEGURIDAD);

#ifdef DEGUG
  Serial.print("PWM Throttle(us): ");
  Serial.print(throttle_pwm);
  Serial.print("   |   PWM Steering(us): ");
  Serial.println(steering_pwm);
#endif
}

void controlFunction(int throttle_pwm, int steering_pwm){

  // Seguridad: si cualquier señal no llega → apagar motores
  if (throttle_pwm == 0 || steering_pwm == 0) {
    //Apaga motores
    L298_Driver(0, 0);
#ifdef DEBUG
  Serial.println("SIN SEÑAL, PARADA EMERGENCIA");
#endif
    return;
  }

  // Convertimos a rango 0–255 para marcha adelante
  int throttle = map(throttle_pwm, 1000, 2000, 0, 255);
  throttle = constrain(throttle, 0, 255);

  int steering = map(steering_pwm, 1000, 2000, -350, 350);
  steering = constrain(steering, -350, 350);

  // Mezcla diferencial
  int VR = throttle - steering;
  int VL = throttle + steering;

  
  VR = constrain(VR, -255, 255);
  VL = constrain(VL, -255, 255);
#ifdef FALSE
  Serial.print("Velocidad Derecha (0-255): ");
  Serial.print(VR);
  Serial.print(" | Velocidad Izquierda (0-255): ");
  Serial.println(VL);
#endif
  // Aplicar a los motores
  L298_Driver(VR, VL);

}

void L298_Driver(int VR, int VL){

#ifdef FALSE
  Serial.print("VR: ");
  Serial.println(VR);
  Serial.print("   |   VL: ");
  Serial.println(VL);
#endif
  // --- Motor derecho ---
  if (VR > MINIMO) {
    // Adelante
    digitalWrite(R_IN1, HIGH);
    digitalWrite(R_IN2, LOW);
    analogWrite(R_ENA, VR);
  }
  else if (VR < -MINIMO) {
    // Atrás
    digitalWrite(R_IN1, LOW);
    digitalWrite(R_IN2, HIGH);
    analogWrite(R_ENA, -VR);   // PWM positivo
  }
  else {
    // Atrás
    digitalWrite(R_IN1, LOW);
    digitalWrite(R_IN2, LOW);
  }

  // --- Motor izquierdo ---
  if (VL > MINIMO) {
    // Adelante
    digitalWrite(L_IN3, HIGH);
    digitalWrite(L_IN4, LOW);
    analogWrite(L_ENB, VL);
  } 
  else if (VL < -MINIMO) {
    // Atrás
    digitalWrite(L_IN3, LOW);
    digitalWrite(L_IN4, HIGH);
    analogWrite(L_ENB, -VL);   // PWM positivo
  }
  else {
    digitalWrite(L_IN3, LOW);
    digitalWrite(L_IN4, LOW);
  }

}